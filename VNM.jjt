options {
  IGNORE_CASE=false;
  MULTI=true;	// This will generate one AST class for each non-suppressed non-terminal
  JJTREE_OUTPUT_DIRECTORY="AST";
  // This will put all your AST classes in the AST directory
  VISITOR=true;
  // This won't be used until the next assignment, but will be needed to make your assignment compile properly
}

PARSER_BEGIN(VNM)

public class VNM {
}

PARSER_END(VNM)

TOKEN_MGR_DECLS : 
{

}

SKIP : {
  " "     
  | "\t"    
  | "\n"    
  | "\r" 
}

TOKEN : 
{
  < LESS: "<">
| < LE:     "<="  >
| < GREAT:  ">"   >
| < GE:     ">="  >
| < EQ:     "=="  >
| < NEQ:    "!="  >
| < IN:     "=in" >
| < NOTIN:  "!in" >
| < PLUS:	  "+"   >
| < MINUS:	"-"   >
| < TIMES:	"*"   >
| < DIV:	  "/"   >
| < AND:	  "&"   >
| < VBAR:	  "|"   >
| < NOT:	  "!"   >
| < ASSGN:	":="   >
| < LRND:	  "("   >
| < LSQU:	  "["   >
| < RRND:	  ")"   >
| < RSQU:	  "]"   >
| < COMMA:	","   >
| < SCOL:	  ";"   >
| < TRUE:	  "#1"  >
| < FALSE:	"#0"  >
| < RANGE:	".."  >
}

TOKEN [IGNORE_CASE]:
{
  < DO:"DO">
| < WHILE:"WHILE">
| < FOR:"FOR">
| < IF:"IF">
| < THEN:"THEN">
| < ELIF:"ELIF">
| < ELSE:"ELSE">
| < FI:"FI">
| < FUNCTION:"FUNCTION">
| < RETURN:"RETURN">
| < END:"END">
| < PRINT:"PRINT">
| < PRINTLN:"PRINTLN">
| < VAR:"VAR">
}

TOKEN : 
{
  < #DIGIT:	["0"-"9"]>
|	< #LOWER:	["a"-"z"]>
|	< #UPPER:	["A"-"Z"]>
|	< #LETTER: ["a"-"z","A"-"Z"]>

// Passes strings1 and strings2.
| < STRING: "\"" (~["\""])* "\"" > 
|	< NUMBER:	(<DIGIT>)+ >
|	< IDNUM:  "#" <LETTER>(<LETTER>|<DIGIT>)* >  
|	< IDBOOL:	"?" <LETTER>(<LETTER>|<DIGIT>)* >
|	< IDVEC:  "v_" (<LETTER>|<DIGIT>)+ >  
}

// Special tokens are saved, but not sent to the parser
SPECIAL_TOKEN : 
{
  <COMMENT_SINGLE: "//" (~["\n","\r"])* ("\n"|"\r"|"\r\n")>
}

// ==================================================
//                  PARSER BEGINS
// ==================================================

SimpleNode start	() #void :
{}
{  S()  { return (SimpleNode) (jjtree.popNode()); }
| < EOF > {throw new ParseException("End of File.");}
}

// entry point to parser. Called by TestVNM.java
void S() #void :
{}
{	statement_LL1() ";"
| LOOKAHEAD(identifier() ":=") assign_stat() ";" 
| expression() ";" | boolean_call() ";"
}

//------------------------    STATEMENTS, BODIES AND CLAUSES -----------------------------------

void statement_LL1() #void :
{}
{	var_decl()
| fn_decl()
| return_stat()
| print_stat()
| println_stat()
| if_stat()
| for_stat()
| while_stat()
}

void statement() #void :
{}
{	statement_LL1()
| LOOKAHEAD(2) fn_call()
| assign_stat()
}

void body()		 :
{}
{	(statement() ";")*
}

void clause()		 :
{}
{	(statement() ";")+
}

//---------------------------   DECLARATIONS ------------------------------------------------

void var_decl()	 :
{}
{	<VAR> ident_list()
}

void fn_decl()		 :
{}
{	<FUNCTION> identifier() "(" (ident_list())? ")" body() <END>
}

// NEW: suppressed node creation here so children go to parent
void ident_list() #void :
{}
{	identifier() ("," identifier())*
}


//---------------------------   FUNCTION CALLS AND RETURNS ----------------------------------

// Separate out function calls that can be used in expressions (numbers and vectors),
// from those used in conditions (boolean).
void fn_call()		 :
{}
{	<IDNUM> "(" (exp_list())? ")"
| <IDVEC> "(" (exp_list())? ")"
}

void boolean_call()		 :
{}
{	<IDBOOL> "(" (exp_list())? ")"
}

void exp_list()	 :
{}
{	expression() ("," expression())*
}

// Since we split function calls by boolean and numerical,
//    we need an explicit condition list
void condition_list() :
{}
{ condition() ("," condition())*
}

void return_stat()	 :
{}
{	<RETURN> returnval()
}

void returnval()	 :
{}
{	LOOKAHEAD(condition()) condition()
| expression() 
}

void print_stat() :
{}
{	<PRINT> print_list()
}

void println_stat() :
{}
{ <PRINTLN> (print_list())?
}

// NEW: added token capture for String and nodified it
void print_list()	 :
{ Token t; }
{	
  (expression() | t=<STRING> {jjtThis.value = t.image;} #string | <IDBOOL>) ("," (expression() | t=<STRING> {jjtThis.value = t.image;} #string | <IDBOOL>))*
}

void assign_stat() :
{}
{ <IDNUM> ":=" expression()
| <IDBOOL> ":=" condition()
| <IDVEC> ":=" vec_const()
}

// NEW: Completely reworked if_stat for the recursion requirement
void if_stat() #If :
{}
{	
  <IF> condition() <THEN> clause() 
  (
    // Handle ELIF as nested IF
    <ELIF> if_stat_internal()
    |
    // Handle ELSE
    <ELSE> clause()
    |
    // Handle NULL (empty else)
    {} #Null
  )
  <FI>
}

// NEW: Helper for nested elifs to ensure 3 children
void if_stat_internal() #If :
{}
{
   condition() <THEN> clause()
   (
     <ELIF> if_stat_internal()
     |
     <ELSE> clause()
     |
     {} #Null
   )
}

void elifs() #void :
{}
{	(<ELIF> condition() <THEN> clause())*
}

void for_stat()	 :
{}
{	<FOR> <IDNUM> <IN> exp_list() <DO> body() <END>
}

void while_stat()	 :
{}
{	<WHILE> condition() <DO> body() <END>
}


//---------------------------   CONDITIONS ---------------------------------------------------

// NEW: Added #Or node with valid condition
void condition() #Or(jjtree.arity() > 1) :
{}
{	and_clause() ("|" and_clause())*
}

// NEW: Added #And node with valid condition
void and_clause() #And(jjtree.arity() > 1) :
{}
{	not_clause() ("&" not_clause())*
}

// syntactic lookahead to distinguish comparisons and conditions.
// NEW: Added #Not node
void not_clause() #void :
{}
{	"!" not_clause() #Not
| LOOKAHEAD(expression() comparator()) comparison() | "(" condition() ")"
| LOOKAHEAD(2) boolean_call()
| bool_simple()
}

// NEW: Added #comparison(3)
void comparison() #comparison(3) :
{}
{	expression() comparator() expression()
}

// NEW: Nodified comparators
void comparator() #void :
{}
{	"<" #LT
| "<=" #LE
| ">" #GT
| ">=" #GE
|"==" #EQ
| "!=" #NEQ
| "=in" #IN
| "!in" #NOTIN
}


//---------------------------   EXPRESSIONS ------------------------------------------------

// NEW: Reworked for ASTsum with pos/neg children and valid condition
void expression() #sum(jjtree.arity() > 1) :
{}
{	
  (
     "+" product() #pos(1)
   | "-" product() #neg(1)
   | product()
  )
  (
     "+" product() #pos(1)
   | "-" product() #neg(1)
  )*
}

// NEW: Reworked for MUL/DIV/MOD binary nodes
void product() #void :
{}
{	term() 
  (
      "*" term() #mul(2) 
    | "/" term() #div(2)
    | "%" term() #mod(2)
  )*
}

void term() #void :
{}
{	"(" expression() ")"
| LOOKAHEAD(2) fn_call()
| simple_term()
}

// NEW: Nodified leaves and capturing values
void simple_term() #void :
{ Token t; }
{	t=<IDNUM> { jjtThis.value = t.image; } #idnum
| t=<IDVEC> { jjtThis.value = t.image; } #idvec
| t=<NUMBER> { jjtThis.value = t.image; } #number
| vec_const() 
}

void vec_const()	 :
{}
{	
  LOOKAHEAD("[" expression()) "[" (exp_list())?
"]"
| "[" (condition_list())? "]"
}

// NEW: Nodified identifiers
void identifier() #void :
{ Token t; }
{	t=<IDNUM> { jjtThis.value = t.image; } #idnum
| t=<IDBOOL> { jjtThis.value = t.image; } #idbool
| t=<IDVEC> { jjtThis.value = t.image; } #idvec
}

// NEW: Nodified boolean constants
void bool_simple() #void :
{ Token t; }
{	t=<IDBOOL> { jjtThis.value = t.image; } #idbool
| <TRUE> #TRUE
| <FALSE> #FALSE
}